//===== 15-3athena Script ====================================
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= 15-3athena Project
//===== Description: ========================================= 
//= All breeder NPCs.
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7: Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8: Added 3rd class renters. [15peaces]
//============================================================ 

// PecoPeco Breeder (for Knights)-------------------------------------------
prontera,55,350,5	script	Peco Peco Breeder#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[Peco Peco Breeder]";
	if (BaseJob == Job_Knight) {
		mes "Welcome.";
		mes "Honorable Knight,";
		mes "would you like to rent";
		mes "a Peco Peco? The rental";
		mes "fee is "+.@price+" zeny.";
		next;
		switch(select("Rent Peco Peco:Cancel")) {
		case 1:
			if (zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "You do not";
				mes "have enough zeny.";
				mes "Are you...";
				mes "bankrupt?";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "I'm sorry, but you're";
				mes "not eligible for this";
				mes "service. Please go learn";
				mes "the Peco Peco Ride skill first.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "You're already";
				mes "mounted on a";
				mes "Peco Peco.";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Peco Peco Breeder]";
			mes "I see.";
			mes "Well then,";
			mes "have a good day.";
			close;
		}
	} else {
		mes "I'm sorry, but these";
		mes "Peco Pecos are only";
		mes "available for Knights";
		mes "and Lord Knights.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)-------------------------------------------
prontera,232,318,3	script	Peco Peco Breeder#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[PecoPeco Breeder]";
	if (BaseJob == Job_Crusader) {
		if(Upper != 1 ) mes "Welcome, Crusader.";
		else mes "Welcome, Paladin.";
		mes "We have a special";
		mes "Peco Peco prepared";
		mes "for you. To rent one";
		mes "will cost "+.@price+" zeny.";
		next;
		switch(select("Rent a PecoPeco:Quit")) {
		case 1:
			if (zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "You do not";
				mes "have enough zeny.";
				mes "If you would like";
				mes "a Peco Peco please";
				mes "bring "+.@price+" zeny...";
				close;
			}

			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "You must first learn";
				mes "to ride a PecoPeco before";
				mes "I can rent one to you.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "You are already";
				mes "mounted on a Peco Peco.";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[PecoPeco Breeder]";
			mes "See you around.";
			close;
		}
	} else {
		mes "What can I do for you?";
		mes "Please be aware that";
		mes "this Peco Peco rental";
		mes "service is strictly for";
		mes "Crusaders and Paladins.";
		close;
	}
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	Falcon Breeder#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Falcon Breeder]";
	if (BaseJob == Job_Hunter) {
		mes "Do you need a Falcon?";
		mes "You can rent your own";
		mes "trusty bird of prey for a";
		mes "fee of just "+.@price+" zeny~";
		next;
		switch(select("Rent Falcon:Cancel")) {
		case 1:
			if (zeny < .@price) {
				mes "[Falcon Breeder]";
				mes "What is this?";
				mes "You don't have";
				mes "enough zeny?!";
				mes "You better start";
				mes "hunting money";
				mes "instead of monsters~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Falcon Breeder]";
				mes "Gosh~";
				mes "Go learn how to";
				mes "manage a Falcon";
				mes "first! I can't rent one";
				mes "to you if you can't";
				mes "handle it, you know.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Falcon Breeder]";
				mes "Um...";
				mes "You already have";
				mes "a Falcon. It's right";
				mes "there, can't you see it?";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Falcon Breeder]";
			mes "W-wait, where're";
			mes "you goin'? These";
			mes "Falcons are top notch,";
			mes "I guarantee it! C'mon, yo~";
			close;
		}
	} else {
		mes "Young fool!";
		mes "Falcons can only";
		mes "be used by Hunters";
		mes "and Snipers, capish?";
		mes "...Heh heh, jealous?";
		close;
	}
}

// Dragon Breeder :: dragon
//============================================================
job3_rune01,88,62,5	script	Dragon Breeder	105,{
	mes "[Dragon Breeder]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune_Knight) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Dragon Breeder]";
				mes "Please learn how to ride a Dragon first.";
				close;
			}
			else if (checkdragon()) {
				mes "[Dragon Breeder]";
				mes "You already have a Dragon.";
				close;
			}
			else if (checkriding()) {
				mes "[Dragon Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Dragon Breeder]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "What are you doing here?";
	mes "Only Rune Knights can rent a Dragon.";
	close;
}

// Dragon/Gryphon Master :: dragon
//============================================================
prontera,130,213,5	script	Riding Creature Master	105,{
	mes "[Riding Creature Master]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune_Knight) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Dragon first.";
				close;
			}
			else if (checkdragon()) {
				mes "[Riding Creature Master]";
				mes "You already have a Dragon.";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Royal_Guard) {
		mes "Welcome. Would you like to rent a Gryphon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Gryphon first.";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "You already have a Gryphon.";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setriding;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "I'm here to provide Rune Knights and Royal Guards with riding creatures.";
	close;
}
geffen,100,55,3	duplicate(Riding Creature Master)	Riding Creature Master#2	105
payon,166,102,5	duplicate(Riding Creature Master)	Riding Creature Master#3	105
aldebaran,133,109,5	duplicate(Riding Creature Master)	Riding Creature Master#4	105
yuno,171,187,3	duplicate(Riding Creature Master)	Riding Creature Master#5	105
rachel,106,130,5	duplicate(Riding Creature Master)	Riding Creature Master#6	105

// Peco removing NPC :: dragon
//============================================================
prontera,125,208,5	script	Peco Peco Remover	105,{
	mes "[Soldier]";
	mes "If you're unable to dismount from a Peco Peco";
	mes "for some unknown reason,";
	mes "please feel free to use my services.";
	if (checkriding()) {
		next;
		mes "[Soldier]";
		mes "You're riding a Peco Peco.";
		mes "Would you like to dismount?";
		next;
		switch(select("Yes:No")) {
		case 1:
			setriding 0;
			mes "[Soldier]";
			mes "Say, how does it feel to";
			mes "step on the ground on your own";
			mes "feet again?";
			close;
		case 2:
			mes "[Soldier]";
			mes "I see. Please feel free to ask me";
			mes "if you change your mind.";
			close;
		}
	}
	close;
}

// Magic Gear Renter :: madogear
//============================================================
-	script	::mgm	-1,{
	mes "[Magic Gear Master]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "Welcome, Mechanic.";
		mes "Would you like to rent a Pushcart or";
		mes "ride a Magic Gear?";
		next;
		switch(select("Rent a Pushcart:Ride a Magic Gear:Cancel")) {
		case 1:
			if (checkcart()) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you already";
				mes "have a Pushcart.";
				close;
			}
			setcart;
			mes "[Magic Gear Master]";
			mes "There you go!";
			close;
		case 2:
			if (checkmadogear()) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you're already";
				mes "riding a Magic Gear.";
				close;
			}
			else if (!getskilllv("NC_MADOLICENCE")) {
				mes "[Magic Gear Master]";
				mes "Please learn the skill to get the Magic Gear License first.";
				close;
			}
			else if(checkriding()) {
				mes "[Magic Gear Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmadogear;
			mes "[Magic Gear Master]";
			mes "Have fun, and please come again!";
			close;
		case 3:
			close;
		}
	}
	mes "How may I help you?";
	mes "Magic Gears are only available for Mechanics.";
	close;
}
prontera,163,178,3	duplicate(mgm)	Magic Gear Master#prt	105
geffen,103,55,5	duplicate(mgm)	Magic Gear Master#gef	105
payon,166,106,5	duplicate(mgm)	Magic Gear Master#pay	105
aldebaran,133,112,5	duplicate(mgm)	Magic Gear Master#alde	105
yuno,167,187,3	duplicate(mgm)	Magic Gear Master#yuno	105
rachel,106,134,5	duplicate(mgm)	Magic Gear Master#ra	105
dicastes01,187,207,3	duplicate(mgm)	Magic Gear Master#dic	105
manuk,273,212,5	duplicate(mgm)	Magic Gear Master#man	105
splendide,180,174,5	duplicate(mgm)	Magic Gear Master#spl	105
mid_camp,242,243,3	duplicate(mgm)	Magic Gear Master#mid	105
