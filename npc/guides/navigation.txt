//===== 15-3athena Script ====================================
//= Navigation Functions
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= 15-3athena Project
//===== Description: ========================================= 
//= Controls the navigation system for Renewal guides.
//= Clients before 2011-10-10 do not support navigation
//= links, and will default to colored or plain text.
//= Note: The clientver-check is replaced by a EP check.
//= Minimum episode is 14.3. This EP was first released in 
//= mid 2010. This client date should support navigation iterface.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Added 'is_clientver' command. [Euphy]
//= 1.2 Ported from rAthena. [15peaces]
//============================================================ 

function	script	F_Navi	{

	if (getbattleflag("feature.episode") < 143 )
		set .@disabled,1;

	// This function takes 0 ~ 3 parameters.
	switch(getargcount()) {
	case 0:	// Check if system is disabled.
		return .@disabled;
	case 1:	// Display information message, if enabled.
		if (!.@disabled) {
			next;
			mes getarg(0);
			mes "When you click on the ^B9062F[location name]^000000, you'll receive the most advanced ^B9062FNavigation^000000 services!";
		}
		return;
	default: // Display navigation link, if enabled; else set text color, if available.
		if (!.@disabled)
			return "<NAVI>[" + getarg(0) + "]<INFO>" + getarg(1) + ",0,000,0</INFO></NAVI>";
		else if (getargcount() == 3)
			return getarg(2)+getarg(0)+"^000000";
		else
			return getarg(0);
	}
}
