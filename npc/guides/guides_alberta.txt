//===== 15-3athena Script ====================================
//= Alberta Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= 15-3athena Project
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the city of Alberta
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed Armory coords, thanks to Freya team
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5 Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.6 Added missing Guide. 
//= (Since this is a renewal only guide I decided to add the renewal version.) [15peaces]
//============================================================ 

alberta,23,238,4	script	Guide#alb::AlbGuide	105,{
	cutin "prt_soldier",2;
	mes "[Alberta Guide]";
	mes "Welcome to Alberta,";
	mes "the Port City. Feel free";
	mes "to ask me if you're having";
	mes "trouble finding anything in";
	mes "town, or if you just need";
	mes "guidance around the city.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("City Guide:Remove Marks from Mini-Map:Notice:Cancel")) {
		case 1:
			mes "[Alberta Guide]";
			mes "Please select";
			mes "a location from";
			mes "the following menu.";
			if (.@compass_check == 0) {
				mes "Would you like me";
				mes "to mark locations";
				mes "on your Mini-Map?";
				next;
				if (select("Yes:No") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Merchant Guild^000000:Weapon Shop:Tool Shop:Inn:Forge:Cancel")) {
				case 1:
					mes "[Alberta Guide]";
					mes "The Merchant Guild";
					mes "handles Job Changes";
					mes "to the Merchant Class,";
					mes "and is located in the";
					mes "southwest corner";
					mes "of Alberta.";
					if (.@compass_check)
						viewpoint 1,33,41,2,0xFF0000;
					break;
				case 2:
					mes "[Alberta Guide]";
					mes "The Weapon Shop";
					mes "can be found in the";
					mes "southern end of Alberta.";
					if (.@compass_check)
						viewpoint 1,117,37,3,0xFF00FF;
					break;
				case 3:
					mes "[Alberta Guide]";
					mes "The Tool Shop";
					mes "is kind of close";
					mes "to the center of";
					mes "Alberta. It shouldn't";
					mes "be too hard to find.";
					if (.@compass_check)
						viewpoint 1,98,154,4,0xFF00FF;
					break;
				case 4:
					mes "[Alberta Guide]";
					mes "There's an Inn";
					mes "at the northern";
					mes "end of Alberta";
					mes "where you can rest.";
					if (.@compass_check)
						viewpoint 1,65,233,5,0xFF00FF;
					break;
				case 5:
					mes "[Alberta Guide]";
					mes "The Forge in Alberta";
					mes "is in the same building";
					mes "as the Merchant Guild.";
					mes "It's to the southwest.";
					if (.@compass_check)
						viewpoint 1,35,41,6,0xFF00FF;
					break;
				case 6:
					mes "[Alberta Guide]";
					mes "Please ask me to ''Remove";
					mes "Marks from Mini-Map'' if you";
					mes "no longer wish to have the";
					mes "location marks displayed";
					mes "on your Mini-Map.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0xFF00FF;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,175,220,6,0xFF00FF;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Alberta Guide]";
			mes "Advances in sorcery and";
			mes "technology have allowed";
			mes "us to update our information";
			mes "system, enabling up to mark";
			mes "locations on your Mini-Map";
			mes "for easier navigation.";
			next;
			mes "[Alberta Guide]";
			mes "Your Mini-Map is located";
			mes "in the upper right corner";
			mes "of the screen. If you can't";
			mes "see it, press the Ctrl + Tab";
			mes "keys or click the ''Map'' button in your Basic Info Window.";
			next;
			mes "[Alberta Guide]";
			mes "On your Mini-Map,";
			mes "click on the ''+'' and ''-''";
			mes "symbols to zoom in and";
			mes "our of your Mini-Map. We";
			mes "hope you enjoy your travels";
			mes "here in the city of Alberta.";
			break;
		case 4:
			mes "[Alberta Guide]";
			mes "Be safe when you";
			mes "travel and don't hesitate";
			mes "to ask me if you have any";
			mes "questions about Alberta.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "prt_soldier",255;
	end;
}
alberta,120,60,3	duplicate(AlbGuide)	Guide#2alb	105

// Renewal Guide
alberta,184,143,4	script	Guide#01alberta::AlbertaGuide	105,{
	cutin "prt_soldier",2;
	mes "[Alberta Guide]";
	mes "Welcome to ^8B4513Alberta^000000,";
	mes "the Port City.";
	mes "Do you need help navigating the city?";
	callfunc("F_Navi","[Alberta Guide]");
	next;
	set .@str$,"Would you like to check any other locations?";
	while (1) {
		switch(select("[ Main Facilities ]:[ Merchants & Helpers ]:[ Destination Ports ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Tool Shop ]:[ Forge ]:[ Weapon & Armor Shop ]:[ Inn ]:[ Beauty Salon ]:[ Merchant Guild ]:Previous Menu")) {
				case 1:
					mes "[Alberta Guide]";
					mes "Let me mark the location of the";
					mes "the "+callfunc("F_Navi","Tool Shop","alberta,98,154","^0000FF");
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,98,154,0,0x0A82FF;
					next;
					break;
				case 2:
					mes "[Alberta Guide]";
					mes "The "+callfunc("F_Navi","Forge","alberta,35,41","^B9062F")+" is currently";
					mes "located inside the Merchant Guild building.";
					mes .@str$;
					viewpoint 1,35,41,1,0xFF0000;
					next;
					break;
				case 3:
					mes "[Alberta Guide]";
					mes "Let me mark the location of the";
					mes "the "+callfunc("F_Navi","Weapon & Armor Shop","alberta,117,37","^FF5400");
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,117,37,2,0xFFB400;
					next;
					break;
				case 4:
					mes "[Alberta Guide]";
					mes "Let me mark the location of the";
					mes "the "+callfunc("F_Navi","Inn","alberta,65,233","^006400");
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,65,233,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[Alberta Guide]";
					mes "Let me mark the location of the";
					mes "the "+callfunc("F_Navi","Beauty Salon","alberta,48,145","^7F3300");
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,48,145,4,0xD2691E;
					next;
					break;
				case 6:
					mes "[Alberta Guide]";
					mes "The "+callfunc("F_Navi","Merchant Guild","alberta,33,41")+" is where";
					mes "you can change your job to ^800080Merchant^000000.";
					mes "Let me mark its location";
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,33,41,5,0xDA70D6;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Eden Teleport Officer ]:[ Kafra Employee ]:[ Cool Event Employee ]:[ Bullet Merchant ]:[ Cooking Ingredient Merchant ]:Previous Menu")) {
				case 1:
					mes "[Alberta Guide]";
					mes "^B9062FEden Teleport Officers^000000 are located in "+callfunc("F_Navi","south Alberta","alberta,121,68");
					mes "and inside the "+callfunc("F_Navi","Merchant Guild","alberta,33,41")+" building.";
					mes "Let me mark their locations on your mini-map.";
					mes .@str$;
					viewpoint 1,33,41,6,0xFF0000;
					viewpoint 1,121,68,7,0xFF0000;
					next;
					break;
				case 2:
					mes "[Alberta Guide]";
					mes "Let me mark the location of the";
					mes "^0000FFKafra Employees^000000";
					mes "on your mini-map.";
					mes .@str$;
					viewpoint 1,33,41,8,0x0A82FF;
					viewpoint 1,113,60,9,0x0A82FF;
					next;
					break;
				case 3:
					mes "[Alberta Guide]";
					mes "When you win event items,";
					mes "you can claim them through the "+callfunc("F_Navi","Cool Event Employee","alberta,148,57","^FF5400")+".";
					mes .@str$;
					viewpoint 1,148,57,10,0xFFB400;
					next;
					break;
				case 4:
					mes "[Alberta Guide]";
					mes "The ^7F3300Bullet Merchants^000000 for ^7F3300Gunslingers^000000";
					mes "are located";
					mes "near the "+callfunc("F_Navi","center of the city","alberta,117,158")+".";
					mes .@str$;
					viewpoint 1,117,158,11,0xD2691E;
					next;
					break;
				case 5:
					mes "[Alberta Guide]";
					mes "The "+callfunc("F_Navi","Chef Assistant","alberta,167,135","^006400")+" in Alberta";
					mes "has many regular customers,";
					mes "especially for his ^006400Delicious Fishes^000000.";
					mes .@str$;
					viewpoint 1,167,135,12,0xAAFF00;
					next;
					break;
				case 6:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Port - Northern Dock ]:[ Port - Central Dock ]:[ Port - Southern Dock ]:Previous Menu")) {
				case 1:
					mes "[Alberta Guide]";
					mes "You can use the "+callfunc("F_Navi","Northern Dock","alberta,192,196");
					mes "to go to ^0000FFPort Malaya^000000 or ^0000FFDewata^000000.";
					mes .@str$;
					viewpoint 1,192,196,13,0xFF0000;
					next;
					break;
				case 2:
					mes "[Alberta Guide]";
					mes "You can use the "+callfunc("F_Navi","Central Dock","alberta,192,151");
					mes "to go to the ^B9062FSunken Ship^000000 or ^B9062FIzlude Marina^000000.";
					mes .@str$;
					viewpoint 1,192,151,14,0xFF0000;
					next;
					break;
				case 3:
					mes "[Alberta Guide]";
					mes "You can use the "+callfunc("F_Navi","Southern Dock","alberta,245,86");
					mes "to go to ^0000FFTurtle Island^000000, ^0000FFBrasilis^000000, ^0000FFAmatsu^000000, ^0000FFKunlun^000000, ^0000FFMoscovia^000000, ^0000FFLuoyang^000000, or ^0000FFAyothaya^000000.";
					mes .@str$;
					viewpoint 1,245,86,15,0xFF0000;
					next;
					break;
				case 4:
					set .@loop,0;
					break;
				}
			}
			break;
		case 4:
			mes "[Alberta Guide]";
			mes "Sure, I'll remove all marks from your mini-map.";
			mes "Is there anything else I can do for you?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0x000000;
			viewpoint 2,1,1,13,0xFFFF00;
			viewpoint 2,1,1,14,0xFFFF00;
			viewpoint 2,1,1,15,0xFFFF00;
			next;
			break;
		case 5:
			mes "[Alberta Guide]";
			mes "Enjoy your stay.";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
}
