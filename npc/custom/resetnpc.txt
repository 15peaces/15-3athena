//===== 15-3athena Script ====================================
//= Reset NPC for Athena by Script & DB Team
//===== By: ================================================== 
//= eAthena Team
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= 15-3athena
//===== Description: ========================================= 
//= Resets skills, stats, or both.
//===== Additional Comments: ================================= 
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning. Thanks to Euphy. [15peaces]
//= 1.3 All statuses removed upon skill reset. Thanks to Euphy. [15peaces]
//============================================================

prontera,150,193,4	script	Reset Girl	124,{

	set .@ResetStat,5000;	// Zeny for stat reset
	set .@ResetSkill,5000;	// Zeny for skill reset
	set .@ResetBoth,9000;	// Zeny for resetting both together

	mes "[Reset Girl]";
	mes "I am the Reset Girl.";
	mes "Reset Stats: "+ callfunc("F_InsertComma",.@ResetStat) +"z";
	mes "Reset Skills: "+ callfunc("F_InsertComma",.@ResetSkill) +"z";
	mes "Reset Both: "+ callfunc("F_InsertComma",.@ResetBoth) +"z";
	mes "Please select the service you want:";
	next;
	switch(select("^FF3355Reset Skills:Reset Stats:Reset Both^000000:Cancel")) {
	case 1:
		mes "[Reset Girl]";
		if (Zeny < .@ResetSkill) {
			mes "Sorry, you don't have enough Zeny.";
			close; }
		set Zeny, Zeny-.@ResetSkill;
		sc_end SC_ALL;
		ResetSkill;
		mes "There you go!";
		close;
	case 2:
		mes "[Reset Girl]";
		if (Zeny < .@ResetStat) {
			mes "Sorry, you don't have enough Zeny.";
			close; }
		set Zeny, Zeny-.@ResetStat;
		ResetStatus;
		mes "There you go!";
		close;
	case 3:
		mes "[Reset Girl]";
		if (Zeny < .@ResetBoth) {
			mes "Sorry, you don't have enough Zeny.";
			close; }
		set Zeny, Zeny-.@ResetBoth;
		sc_end SC_ALL;
		ResetSkill;
		ResetStatus;
		mes "There you go!";
		close;
	case 4:
		close;
	}
}
