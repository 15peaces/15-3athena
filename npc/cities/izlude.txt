//===== 15-3athena Script ==================================== 
//= Izlude Town
//===== By: ================================================== 
//= kobra_k88
//= L0ne_W0lf
//= 15peaces
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= 15-3athena Project
//===== Description: ========================================= 
//= Izlude town NPCs
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 fixed 2 zeny bugs/checks [Lupus]
//= 1.2 Fixed a lot of typos [Nexon]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Added a Jawaii related npc. 1.4a Fixed a small bug to
//=	the Jawaii warper, thanks to reddozen and Silent [MasterOfMuppets]
//= 1.5 Replaced iz_move_alberta with MISC_QUEST & 16 [Lupus]
//= 1.6 Removed Duplicates [Silent]
//= 1.7 Split quest to quests/quests_izlude.txt [Evera]
//= 1.71 Optimized. Also thanks to CAHTEXHuK [Lupus]
//= 1.72 Small bugfix [Paradox924X]
//= 1.8 Rescripted to Aegis 10.3 standard. [L0ne_W0lf]
//=	Removed Honeymoon Helper, as she is a Jawaii NPC.
//=	Moved Signs to the msg_boards file.
//= 1.8a Made Red/Cebalis as a single NPC + duplicate [Lupus]
//= 1.9 Fixed a small problem with Dega. (Ne default case) [L0ne_W0lf]
//=	Re-added NPC dialog for Cebalis. The dialog differs
//=	(even if slightly,) after the five or so lines.
//= 1.9a Updated to renewal map. (rAthena v. 1.0)[15peaces]
//= 2.0 Added Channel Warper and Izlude duplicates. [15peaces]
//============================================================

-	script	::Sailor_izlude	-1,{
	mes "[Sailor]";
	mes "Hey everybody!";
	mes "Attention, attention!";
	mes "Come and ride the wind";
	mes "on a fascinating Ship!";
	mes "Hurry, hurry!";
	next;
	switch(select("Byalan Island -> 150 Zeny.:Alberta Marina  -> 500 Zeny.:Cancel.")) {
	case 1:
		if (zeny < 150) {
			mes "[Sailor]";
			mes "150 Zeny!";
			mes "Only 150 Zeny to ride!";
			close;
		}
		set zeny,zeny-150;
		warp "izlu2dun",107,50;
		end;
	case 2:
		if (zeny < 500) {
			mes "[Sailor]";
			mes "500 Zeny!";
			mes "Only 500 Zeny to ride!";
			close;
		}
		set zeny,zeny-500;
		warp "alberta",188,169;
		end;
	case 3:
		close;
	}
}

izlude,197,205,1	duplicate(Sailor_izlude)	Sailor#izlude	100
izlude_a,197,205,1	duplicate(Sailor_izlude)	Sailor#izlude_a	100
izlude_b,197,205,1	duplicate(Sailor_izlude)	Sailor#izlude_b	100
izlude_c,197,205,1	duplicate(Sailor_izlude)	Sailor#izlude_c	100
izlude_d,197,205,1	duplicate(Sailor_izlude)	Sailor#izlude_d	100

izlu2dun,108,27,0	script	Sailor#2	100,{
	mes "[Sailor]";
	mes "Wanna";
	mes "head back?";
	next;
	if(select("Yeah, I'm tired to death.:Nope, I love this place!") == 1) {
		warp "izlude",197,210;
		end;
	}
	close;
}

-	script	::Bonne_izlude	-1,{
	mes "[Bonne]";
	mes "Greetings!";
	mes "Izlude welcomes you.";
	next;
	mes "[Bonne]";
	mes "Izlude is the satellite city of Prontera, capital of the Rune-Midgarts Kingdom.";
	next;
	mes "[Bonne]";
	mes "Izlude is key to our kingdom because of the Swordsman Association located here, as well as the fact that Izlude is in charge of protecting the Rune-Midgard coastline.";
	next;
	mes "[Bonne]";
	mes "I know, this bridge might look weak and fragile, but it is actually state of the art, built with the most sophisticated technology.";
	next;
	mes "[Bonne]";
	mes "No matter how strong storms may be, or how many people may stand on it, this bridge will NEEEEVER collapse.";
	next;
	mes "[Bonne]";
	mes "Please enjoy";
	mes "your visit";
	mes "here in Izlude.";
	close;
}

izlude,71,92,2	duplicate(Bonne_izlude)	Bonne#izlude	90
izlude_a,71,92,2	duplicate(Bonne_izlude)	Bonne#izlude_a	90
izlude_b,71,92,2	duplicate(Bonne_izlude)	Bonne#izlude_b	90
izlude_c,71,92,2	duplicate(Bonne_izlude)	Bonne#izlude_c	90
izlude_d,71,92,2	duplicate(Bonne_izlude)	Bonne#izlude_d	90

-	script	::Charfri_izlude	-1,{

	switch (rand(1,2)) {
		case 1:
			mes "[Charfri]";
			mes "Some people may think Izlude is just a satellite city of Prontera, and not really that important...";
			next;
			mes "[Charfri]";
			mes "But Izlude is a beautiful town right next to the ocean, as well as beautiful Byalan Island.";
			next;
			mes "[Charfri]";
			mes "You'll have to board on a ship at the port to get to Byalan Island. There are dangerous dungeons on that island, so don't go snooping around just anywhere.";
			close2;
			break;
		case 2:
			mes "[Charfri]";
			mes "Though it is very beautiful, Byalan Island has a mysterious dungeon that extends deep under the sea.";
			next;
			mes "[Charfri]";
			mes "People who've actually been there have said that if you go deep enough, the dungeon actually descends underwater.";
			next;
			mes "[Charfri]";
			mes "Surprisingly, once you're underwater, you can breathe just like a fish. Maybe some kind of supernatural force is in effect.";
			next;
			mes "[Charfri]";
			mes "Ah...";
			mes "The people who've seen the underwater view say it is so fantastic that they've kept dreaming of it ever since.";
			next;
			mes "[Charfri]";
			mes "But unfortunately, the monsters are too strong for ordinary people to merely go sight seeing there. Still, just once, I'd like to go down there...";
			close2;
			break;
		}
	end;
}

izlude,172,215,4	duplicate(Charfri_izlude)	Charfri#izlude	91
izlude_a,172,215,4	duplicate(Charfri_izlude)	Charfri#izlude_a	91
izlude_b,172,215,4	duplicate(Charfri_izlude)	Charfri#izlude_b	91
izlude_c,172,215,4	duplicate(Charfri_izlude)	Charfri#izlude_c	91
izlude_d,172,215,4	duplicate(Charfri_izlude)	Charfri#izlude_d	91

-	script	::Cuskoal_izlude	-1,{

	switch (rand(1,2)) {
		case 1:
			mes "[Cuskoal]";
			mes "The Arena here is THE place for capable young people from all over the Rune-Midgarts Kingdom to challenge themselves and test their skills.";
			next;
			mes "[Cuskoal]";
			mes "You can battle with monsters of differing levels. So, the number of stages you survive will be a testament to your battle prowess.";
			next;
			mes "[Cuskoal]";
			mes "So, whaddya say?";
			close;
		case 2:
			mes "[Cuskoal]";
			mes "The pubs in Prontera are always full of people from local areas and from out-of-town. It can get pretty busy.";
			next;
			mes "[Cuskoal]";
			mes "It's a pretty good place to stop by for general information and to listen to rumors.";
			next;
			mes "[Cuskoal]";
			mes "So if you listen carefully, you just might get lucky and learn some very useful information for yourself.";
			close;
	}
}

izlude,174,164,4	duplicate(Cuskoal_izlude)	Cuskoal#izlude	124
izlude_a,174,164,4	duplicate(Cuskoal_izlude)	Cuskoal#izlude_a	124
izlude_b,174,164,4	duplicate(Cuskoal_izlude)	Cuskoal#izlude_b	124
izlude_c,174,164,4	duplicate(Cuskoal_izlude)	Cuskoal#izlude_c	124
izlude_d,174,164,4	duplicate(Cuskoal_izlude)	Cuskoal#izlude_d	124

-	script	::Dega_izlude	-1,{
	switch (rand(1,3)) {
		case 1:
			mes "[Dega]";
			mes "Mt. Mjornir, located north of Prontera, is a tough";
			mes "and steep climb.";
			next;
			mes "[Dega]";
			mes "Aside from the dangers of the mountain itself, insanely vicious insects live there too. I mean, they'll just attack you for no reason.";
			next;
			mes "[Dega]";
			mes "If you ever want to visit somewhere past Mt. Mjolnir, then you prepare yourself for the challenge.  Or you could walk around it.";
			close;
		case 2:
			mes "[Dega]";
			mes "Some monsters in the world have the unique ability to sense mystical energy, and can detect Magic spells before they are cast.";
			next;
			mes "[Dega]";
			mes "Golem of the desert is one of them. Don't underestimate it due to its sluggishness...";
			next;
			mes "[Dega]";
			mes "If you try to cast magic near it, it will notice and saunter over to smash you. So you better watch out for Golem.";
			close;
		case 3:
			mes "[Dega]";
			mes "There's a very delightful place where you can find every";
			mes "type of Poring.";
			next;
			mes "[Dega]";
			mes "It's somewhere near the bridge connecting the forest and the desert, on the way to the city of Payon which is Southeast from here.";
			next;
			mes "[Dega]";
			mes "There are not only pink Porings but also Drops, which can be found at the desert, and the green Poporing.";
			next;
			mes "[Dega]";
			mes "But be careful, before you realize it, you may come face to face with Ghostring, a deadly Poring that floats around in the air like a ghost.";
			next;
			mes "[Dega]";
			mes "Well, of course, they are all very cute, but Ghostring is an EXCEPTION. It is very very dangerous.";
			next;
			mes "[Dega]";
			mes "If you are lucky enough, you might even bump into Angelring, the Poring with Angel wings.";
			next;
			while(1) {
				switch(select("Ghostring?:Angelring?:End Conversation.")) {
				case 1:
					mes "[Dega]";
					mes "Ghostring is a grayish Poring that floats around in the air like a ghost. Just like other ghosts, physical attacks can't do any damage to it.";
					next;
					mes "[Dega]";
					mes "Those whose main attack methods are physical like Swordman and Archer might have to run for their lives when facing Ghostrings.";
					next;
					mes "[Dega]";
					mes "But don't leave just yet~! There is great news for people with those jobs. Making a weapon of some elemental property is the key.";
					next;
					mes "[Dega]";
					mes "This way, even a Swordman or an Archer can inflict damage, the way Magic does, on Ghostrings.";
					next;
					break;
				case 2:
					mes "[Dega]";
					mes "Angelrings are immune to Magic attacks. If people who can only attack with Magic face an Angelring, then it's time for";
					mes "them to run.";
					next;
					mes "[Dega]";
					mes "If you've got an extra knife or sword, you could give it a shot. But it will be very difficult alone, don't you think?";
					next;
					break;
				case 3:
					mes "[Dega]";
					mes "Good Luck~";
					close;
				}
			}
	}
}

izlude,85,103,4	duplicate(Dega_izlude)	Dega#izlude	84
izlude_a,85,103,4	duplicate(Dega_izlude)	Dega#izlude_a	84
izlude_b,85,103,4	duplicate(Dega_izlude)	Dega#izlude_b	84
izlude_c,85,103,4	duplicate(Dega_izlude)	Dega#izlude_c	84
izlude_d,85,103,4	duplicate(Dega_izlude)	Dega#izlude_d	84

-	script	::Kylick_izlude	-1,{
	switch (rand(1,2)) {
		case 1:
			mes "[Kylick]";
			mes "Don't you think Binoculars";
			mes "are really COOL?! You can";
			mes "see all sorts of places...!";
			next;
			mes "[Kylick]";
			mes "Here in Izlude, we are responsible for maintaining peace not only on land but also at sea. That's why this city has a huge telescope.";
			next;
			mes "[Kylick]";
			mes "This telescope constantly watches over the sea, so that we can prevent any serious trouble from happening. You know...";
			next;
			mes "[Kylick]";
			mes "An ounce of";
			mes "prevention is worth";
			mes "a pound of cure";
			mes "after all, right?";
			close;
		case 2:
			mes "[Kylick]";
			mes "I was thinking, even though the people of Izlude live so close to the ocean...";
			next;
			mes "[Kylick]";
			mes "There are other cultures that have completely developed by living off of the sea. Of course, I'm talking about Amatsu.";
			next;
			mes "[Kylick]";
			mes "I hear the cuisine there is really good! Although the idea of eating raw fish is new to me, I would love to go there, and try it just once!";
			close;
	}
}

izlude,140,186,4	duplicate(Kylick_izlude)	Kylick#izlude	97
izlude_a,140,186,4	duplicate(Kylick_izlude)	Kylick#izlude_a	97
izlude_b,140,186,4	duplicate(Kylick_izlude)	Kylick#izlude_b	97
izlude_c,140,186,4	duplicate(Kylick_izlude)	Kylick#izlude_c	97
izlude_d,140,186,4	duplicate(Kylick_izlude)	Kylick#izlude_d	97

-	script	::Red_izlude	-1,{
	mes "[Red]";
	mes "The only skill that's needed for a Swordman is ^FF2400Bash^000000! Bash, Bash and ONLY ^FF2400Bash^000000! No need to waste time and effort for smaller skills! Everything else is for cowards and wusses!";
	next;
	mes "[Cebalis]";
	mes "What are you talking about!? The ideal Swordman resolutely stands alone, surrounded by countless enemies and smashing them all with one awesome attack.";
	next;
	mes "[Cebalis]";
	mes "^EE0000MAGNUM BREAK!^000000";
	mes "That's right, Magnum Break";
	mes "is the skill that does";
	mes "the job right~!!";
	next;
	mes "[Cebalis]";
	mes "Well... Sometimes the explosive damage might accidentally hit some wandering monsters, and those guys end up coming after you, but that's a risk a Swordman should be willing to take!!";
	next;
	mes "[Red]";
	mes "That's exactly why you're dumb, you idiot! And what's this about the 'the ideal Swordman?' I still remember the last time you used Magnum Break...";
	next;
	mes "[Red]";
	mes "You ended up running away from all those monsters you hit with that stupid skill! Weakling! All those Porings around you got hit and they all tried to kill you. ";
	next;
	mes "[Cebalis]";
	mes "Hmpf. As I recall, you were running away too, apparently too busy to use your precious Bash. In any case, Magnum Break is THE skill for a Swordman~!!";
	next;
	mes "[Cebalis]";
	mes "Something simplistic like Bash";
	mes "is just one of the little steps towards Magnum Break.";
	next;
	mes "[Red]";
	mes "Oh man~";
	mes "Hey, I know you just";
	mes "heard everything.";
	mes "So what do you think?";
	next;
	mes "[Red]";
	mes "Which one do you think is better? The critical damage skill, ^FF2400Bash^000000, or the Splash damage skill, ^EE0000Magnum Break^000000?";
	next;
	switch (select("Bash:Magnum Break")) {
	case 1:
		if (Class == Job_Swordman || Class == Job_Knight || Class == Job_Lord_Knight || Class == Job_Crusader || Class == Job_Paladin || Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Royal_Guard || Class == Job_Royal_Guard_T) {
			mes "[Red]";
			mes "Hahahaha!!! I knew you'd see things my way!! You ARE a great guy!! Undoubtedly, only ^FF2400Bash^000000 suits a Swordman. Please tell that to this BONEHEAD over here~ Hahaha!";
			next;
			mes "[Red]";
			mes "Hmm, let me give you a bit of advice. After you achieve level 5 'Bash', the amount of SP consumed by the skill increases greatly, so watch out for your SP.";
			close;
		}
		mes "[Red]";
		mes "Hahahaha!! See!? Someone who pursues a different job agrees with me~! You really are a great guy! Hahaha!! Undoubtedly, only ^FF2400Bash^000000 suits a Swordman. Please tell that to this NIMROD over here~ Hahaha!";
		close;
	case 2:
		if (Class == Job_Swordman || Class == Job_Knight || Class == Job_Lord_Knight || Class == Job_Crusader || Class == Job_Paladin || Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Royal_Guard || Class == Job_Royal_Guard_T) {
			mes "[Cebalis]";
			mes "Alright!! ^EE0000Magnum Break^000000 is the BEST!! Now you're talking~!! You know the stuff~ HaHaHa!";
			next;
			mes "[Cebalis]";
			mes "You wanna know some useful information? Okay, okay lemme tell ya! Magnum Break has Fire Property.";
			next;
			mes "[Cebalis]";
			mes "So it won't be too effective against monsters with the Water property, but this is THE skill to use against Undead and Earth property monsters!";
			next;
			mes "[Cebalis]";
			mes "And most importantly, look around before you use it. Otherwise you'll be in BIG trouble~ ";
			close;
		}
		mes "[Cebalis]";
		mes "Right?! ^EE0000Magnum Break^000000 is THE BEST!!! You know what you're talking about, eh? I don't know why this jerkface is being sooooo stubborn.";
		close;
	}
}

izlude,57,159,2	duplicate(Red_izlude)	Red#izlude	85
izlude_a,57,159,2	duplicate(Red_izlude)	Red#izlude_a	85
izlude_b,57,159,2	duplicate(Red_izlude)	Red#izlude_b	85
izlude_c,57,159,2	duplicate(Red_izlude)	Red#izlude_c	85
izlude_d,57,159,2	duplicate(Red_izlude)	Red#izlude_d	85

-	script	::Cebalis_izlude	-1,{
	mes "[Red]";
	mes "The only skill that's needed for a Swordman is ^FF2400Bash^000000! Bash, Bash and ONLY ^FF2400Bash^000000! No need to waste time and effort on smaller skills~~ Everything else is for cowards and wusses!";
	next;
	mes "[Cebalis]";
	mes "What are you talking about!? The ideal Swordman resolutely stands alone, surrounded by countless foes, smashing them all with one awesome attack...";
	next;
	mes "[Cebalis]";
	mes "^EE0000MAGNUM BREAK!^000000";
	mes "That's right, Magnum Break";
	mes "is the perfect";
	mes "Swordman skill.";
	next;
	mes "[Cebalis]";
	mes "Well...";
	mes "Sometimes the explosion accidentally hits some monsters that are just wandering around.";
	next;
	mes "[Cebalis]";
	mes "Then they all end up coming after you, but that's a risk a true Swordman should be willing to take.";
	next;
	mes "[Red]";
	mes "That's exactly why you're dumb, you idiot! And what was that about the 'ideal Swordman?' You remember the last time you used Magnum Break?!";
	next;
	mes "[Red]";
	mes "You had to run away from all those Porings hit by that stupid skill! You weakling! All those Porings that you hit tried to kill you! ";
	next;
	mes "[Cebalis]";
	mes "Oh shut up. And those were Poporings. As I recall, you were running away too, apparently too busy to use your precious Bash.";
	next;
	mes "[Cebalis]";
	mes "In any case, Magnum Break is THE skill for a Swordman~!! Something simplistic like Bash is just one of those little steps towards Magnum Break.";
	next;
	mes "[Red]";
	mes "Oh man~";
	mes "Hey, I know you";
	mes "heard everything.";
	mes "So what do you think?";
	next;
	mes "[Red]";
	mes "Which one do you think is better? The critical damage skill, ^FF2400Bash^000000, or the Splash damage skill, ^EE0000Magnum Break^000000?";
	next;
switch (select("Bash:Magnum Break")) {
	case 1:
		if (Class == Job_Swordman || Class == Job_Knight || Class == Job_Lord_Knight || Class == Job_Crusader || Class == Job_Paladin || Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Royal_Guard || Class == Job_Royal_Guard_T) {
			mes "[Red]";
			mes "Hahahaha!!! I knew you'd see things my way!! You ARE a great guy!! Without a doubt, only ^FF2400Bash^000000 suits a Swordman. Please tell that to FUNBOY over here!! Hahaha.";
			next;
			mes "[Red]";
			mes "Hmm, let me give you a bit of advice. After you achieve level 5 Bash, the amount of SP consumed by the skill increases greatly, so watch out for your SP.";
			close;
		}
		mes "[Red]";
		mes "Hahahaha!!";
		mes "See!? Someone who pursues a different job agrees with me~! You really are a great guy! Hahaha!!";
		next;
		mes "[Red]";
		mes "Without a doubt, only ^FF2400Bash^000000 suits a Swordman. Please tell that to this MORON over here!! Hahaha~";
		close;
	case 2:
		if (Class == Job_Swordman || Class == Job_Knight || Class == Job_Lord_Knight || Class == Job_Crusader || Class == Job_Paladin || Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Royal_Guard || Class == Job_Royal_Guard_T) {
			mes "[Cebalis]";
			mes " Alright!! ^EE0000Magnum Break^000000 is the BEST!! Now you're talking~!! You know your stuff, kid. HaHaHa~!";
			next;
			mes "[Cebalis]";
			mes "You wanna know some useful information? Okay, okay lemme tell ya! The explosion from 'Magnum Break' has the Fire Property.";
			next;
			mes "[Cebalis]";
			mes "So it won't be very effective against Water property monsters, but this is THE skill to use against Undead and Earth property monsters.!";
			next;
			mes "[Cebalis]";
			mes "And most importantly, look around before you use it. Otherwise you'll be in BIG trouble~ ";
			close;
		}
		mes "[Cebalis]";
		mes "I'm right, aren't I?! ^EE0000Magnum Break^000000 is THE BEST!!! You know what you're talking about, eh? I don't know why this LARDFACE is sooooo stubborn.";
		close;
	}
}

izlude,55,159,2	duplicate(Cebalis_izlude)	Cebalis#izlude	98
izlude_a,55,159,2	duplicate(Cebalis_izlude)	Cebalis#izlude_a	98
izlude_b,55,159,2	duplicate(Cebalis_izlude)	Cebalis#izlude_b	98
izlude_c,55,159,2	duplicate(Cebalis_izlude)	Cebalis#izlude_c	98
izlude_d,55,159,2	duplicate(Cebalis_izlude)	Cebalis#izlude_d	98

izlude_in,125,164,2	script	Aaron	65,{
	mes "[Aaron]";
	mes "Don't you think Strong VIT and training in a unique breathing method which enables quick HP recovery are the greatest advantages for a Swordman?";
	next;
	mes "[Aaron]";
	mes "If you train your skills very hard, you can even see your HP recovering. The amount";
	mes "recovered depends";
	mes "vitality, or VIT.";
	next;
	mes "[Aaron]";
	mes "So if you invest more of your stats in VIT, you'll recover more HP overall when resting.";
	next;
	mes "[Aaron]";
	mes "But of course, it'd be good to have high Attack, wouldn't it? You can either acquire a good weapon or bring up your STR to support you Attack.";
	next;
	mes "[Aaron]";
	mes "You know you'll need some strength anyway to swing good weapons easily, anyway.";
	next;
	mes "[Aaron]";
	mes "Another important thing is how accurate you can hit your opponents. DEX is the key here. If you train DEX, then the gap between the MIN and MAX damage will also decrease.";
	next;
	mes "[Aaron]";
	mes "Hm...";
	mes "Are you bored by all this talk? Or do you want me to go on?";
	next;
	switch (select("Tell me more please.:End conversation.")) {
		case 1:
			mes "[Aaron]";
			mes "Hmm...";
			mes "In that case, I'll explain about the other attributes to you briefly. In order to attack and evade quickly, you've gotta pay attention to AGI. ";
			next;
			mes "[Aaron]";
			mes "In case you want to make more critical hits, it's a good idea to invest in LUK. INT also increases Max SP, which is needed to use various skills... But it's really up to you.";
			close;
		case 2:
			mes "[Aaron]";
			mes "Okay then,";
			mes "train hard~~";
			close;
	}
}

-	script	::Soldier_izlude	-1,{
	mes "[Soldier]";
	mes "HeHeHeHe..HaHaHaHa ";
	mes "Huh? Why am I so happy?";
	mes "You wanna know?";
	next;
	switch (select("Sure, why?:Not really, I don't care.")) {
	case 1:
		mes "[Soldier]";
		mes "Ah~~ There's not much for us to do these days. You see, Merchants buy items dropped by monsters. But you knew that, right? ";
		next;
		switch (select("Of course:Eh? Really?")) {
		case 1:
			mes "[Soldier]";
			mes "HaHa~ In fact, that was actually part of our job. But there were more and more hunters who came to us in order to get paid and it became too much to handle.";
			next;
			mes "[Soldier]";
			mes "We had to work overtime every day. Ah, it was a nightmare...! Anyway, the government eventually made a wise decision in creating the Registration System.";
			next;
			mes "[Soldier]";
			mes "The Office of Prize Compensation only pays those who have the Registration. Of course, you'd have to be a merchant and stay in the same place all day long.";
			next;
			mes "[Soldier]";
			mes "The Office gives away the registration to any merchant who fulfills those requirements. So nowadays, the hunters sell their goods to the registered merchants.";
			next;
			mes "[Soldier]";
			mes "So nowadays, the hunters sell their goods to the registered merchants.  Not too many people come to us for that anymore.";
			next;
			mes "[Soldier]";
			mes "I mean we are still busy, but that's nothing compared to how it was before. People who have felt suffering know how to appreciate even the slightest comfort.";
			close;
		case 2:
			mes "[Soldier]";
			mes "What?! What do you mean you didn't know?! Well, you know you can get items by killing monsters. If you bring and sell those to a merchant, you can make some money. ";
			next;
			mes "[Soldier]";
			mes "HaHa, in fact, that used to be part of our job. But there were more and more hunters who come in order to get paid, so it became too much to handle.";
			next;
			mes "[Soldier]";
			mes "We had to work overtime every day. Ah, it was a nightmare...! Anyway, the government eventually made a wise decision in creating the Registration System.";
			next;
			mes "[Soldier]";
			mes "The Office of Prize Compensation only pays those who have the Registration. Of course, you'd have to be a merchant and stay in the same place all day long.";
			next;
			mes "[Soldier]";
			mes "The Office gives away the registration to any merchant who fulfills those requirements. So nowadays, the hunters sell their goods to the registered merchants.";
			next;
			mes "[Soldier]";
			mes "So nowadays, the hunters sell their goods to the registered merchants.  Not too many people come to us for that anymore.";
			next;
			mes "[Soldier]";
			mes "I mean we are still busy, but that's nothing compared to how it was before. People who have felt suffering know how to appreciate even the slightest comfort.";
			close;
		}
	case 2:
		mes "[Soldier]";
		mes "Okay Good Bye~~";
		close;
	}
}

izlude,165,156,2	duplicate(Soldier_izlude)	Soldier#izlude	105
izlude_a,165,156,2	duplicate(Soldier_izlude)	Soldier#izlude_a	105
izlude_b,165,156,2	duplicate(Soldier_izlude)	Soldier#izlude_b	105
izlude_c,165,156,2	duplicate(Soldier_izlude)	Soldier#izlude_c	105
izlude_d,165,156,2	duplicate(Soldier_izlude)	Soldier#izlude_d	105

izlude,147,131,6	script	Blacksmith#izl	726,{
	end;
}

-	script	::Guard_izlude	-1,{
	mes "[Izlude Guard]";
	mes "Welcome to Izlude.";
	close;
}

prt_fild08,350,219,4	duplicate(Guard_izlude)	Guard#izlude	105
prt_fild08a,350,219,4	duplicate(Guard_izlude)	Guard#izlude_a	105
prt_fild08b,350,219,4	duplicate(Guard_izlude)	Guard#izlude_b	105
prt_fild08c,350,219,4	duplicate(Guard_izlude)	Guard#izlude_c	105
prt_fild08d,350,219,4	duplicate(Guard_izlude)	Guard#izlude_d	105

// Channel Warper
//============================================================
function	script	F_IzludeChannel	{
	mes "[Channel Warp Official]";
	mes "Izlude is a verly lively place is it not?";
	mes "I am here to help make Izlude feel more smooth!";
	mes "I can send you to another copy of Izlude.";
	mes "Do you want to go to another copy?";
	next;
	set .@i, select(getarg(0)+":Never mind");
	if (.@i < 6) {
		setarray .@maps$[1],"izlude","izlude_a","izlude_b","izlude_c","izlude_d";
		savepoint .@maps$[.@i],128,98;
		warp .@maps$[.@i],128,98;
		end;
	}
	close;
}
izlude,122,105,5	script	Channel Warp Official	64,{ callfunc "F_IzludeChannel",":Go to copy 2:Go to copy 3:Go to copy 4:Go to copy 5"; }
izlude_a,122,105,5	script	Channel Warp Official#_a	64,{ callfunc "F_IzludeChannel","Go to copy 1::Go to copy 3:Go to copy 4:Go to copy 5"; }
izlude_b,122,105,5	script	Channel Warp Official#_b	64,{ callfunc "F_IzludeChannel","Go to copy 1:Go to copy 2::Go to copy 4:Go to copy 5"; }
izlude_c,122,105,5	script	Channel Warp Official#_c	64,{ callfunc "F_IzludeChannel","Go to copy 1:Go to copy 2:Go to copy 3::Go to copy 5"; }
izlude_d,122,105,5	script	Channel Warp Official#_d	64,{ callfunc "F_IzludeChannel","Go to copy 1:Go to copy 2:Go to copy 3:Go to copy 4:"; }

// Talking NPCs
//============================================================
izlude,143,205,7	script	Talkative Kid#iz	703,{
	end;
OnTimer64000:
	npctalk "I used all my Novice Red Potions. What should I do?";
	end;
OnTimer72000:
	npctalk "Where is the shop?";
	end;
OnTimer136000:
	npctalk "Idiot! You do not know about the 'shortcut window'?";
	end;
OnTimer144000:
	npctalk "The items in your inventory can be dragged and put in the shortcut window.";
	end;
OnTimer212000:
	npctalk "I got so many beginner's items in the Academy!";
	end;
OnTimer276000:
	npctalk "They even gave me weapons and a shield!";
	end;
OnTimer284000:
	npctalk "Just press  <Ctrl+~key> to open the map you have.";
	end;
OnTimer344000:
	npctalk "Then do you want to go Criatura Academy with me? I'm learning how to battle from Instructor Subino!";
	end;
OnTimer348000:
	npctalk "The Academy is in North side of Izlude. Let's go~!";
	end;
OnTimer416000:
	npctalk "It's a system to guide you to a destination.";
	end;
OnTimer424000:
	npctalk "In the basic menu, please click the [Navigation] button.";
	end;
OnTimer492000:
	npctalk "Whee, Ain't gonna use your brain? Please type /where in the chat window now!";
	end;
OnTimer552000:
	npctalk "In this case, you can push the <Enter> key.";
	end;
OnTimer560000:
	npctalk "We are just repeating the same dialog. HaHaha.";
	end;
OnTimer565000:
OnInit:
	initnpctimer;
	end;
}
izlude_a,143,205,7	duplicate(Talkative Kid#iz)	Talkative Kid#iza	703
izlude_b,143,205,7	duplicate(Talkative Kid#iz)	Talkative Kid#izb	703
izlude_c,143,205,7	duplicate(Talkative Kid#iz)	Talkative Kid#izc	703
izlude_d,143,205,7	duplicate(Talkative Kid#iz)	Talkative Kid#izd	703
izlude,146,205,7	script	Talkative Adventurer#iz	706,{
	end;
OnTimer69000:
	npctalk "You can buy potion from Merchants.";
	end;
OnTimer77000:
	npctalk "It's the Potion image in the mini map.";
	end;
OnTimer131000:
	npctalk "Is there any way to use items easily?";
	end;
OnTimer141000:
	npctalk "Shortcut window? The slots at the top of the screen? How can I use it?";
	end;
OnTimer149000:
	npctalk "For inventory, do you mean 'item' menu? Oh, it is!";
	end;
OnTimer217000:
	npctalk "I have a bunch of potions?";
	end;
OnTimer281000:
	npctalk "What?! I should go to the Academy now!";
	end;
OnTimer279000:
	npctalk "Where is Izlude? In the center?";
	end;
OnTimer339000:
	npctalk "I do not know what to do.";
	end;
OnTimer349000:
	npctalk "Oh yea? I want to know so many things~ I will go with you!";
	end;
OnTimer411000:
	npctalk "Navigation, What is that?";
	end;
OnTimer421000:
	npctalk "Oh-! Should I search for a Kafra Employee?";
	end;
OnTimer487000:
	npctalk "What is the town name?";
	end;
OnTimer497000:
	npctalk "I cannot see the part where I can write something in the chat window?";
	end;
OnTimer557000:
	npctalk "Oh you smarty...";
	end;
OnTimer555000:
	npctalk "A while ago, one Novice talked to me like hey 'Talkative Adventurer'?. HaHaha.";
	end;
OnTimer565000:
OnInit:
	initnpctimer;
	end;
}
izlude_a,146,205,7	duplicate(Talkative Adventurer#iz)	Talkative Adventurer#iza	706
izlude_b,146,205,7	duplicate(Talkative Adventurer#iz)	Talkative Adventurer#izb	706
izlude_c,146,205,7	duplicate(Talkative Adventurer#iz)	Talkative Adventurer#izc	706
izlude_d,146,205,7	duplicate(Talkative Adventurer#iz)	Talkative Adventurer#izd	706
prt_fild08,338,217,7	script	Resting Adventurer#iz	882,{
	end;
OnTimer60000:
	npctalk "You know what? If you are sitting, HP and SP recovery is faster.";
	end;
OnTimer65000:
	npctalk "Sitting is possible if your basic job skill level is more than 3. Shortcut is pressing the <INSERT> key.";
OnInit:
	initnpctimer;
	end;
}
prt_fild08a,338,217,7	duplicate(Resting Adventurer#iz)	Resting Adventurer#iza	882
prt_fild08b,338,217,7	duplicate(Resting Adventurer#iz)	Resting Adventurer#izb	882
prt_fild08c,338,217,7	duplicate(Resting Adventurer#iz)	Resting Adventurer#izc	882
prt_fild08d,338,217,7	duplicate(Resting Adventurer#iz)	Resting Adventurer#izd	882
